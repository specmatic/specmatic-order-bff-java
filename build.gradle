import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.2.21'
    id 'org.springframework.boot' version '3.5.7'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.2.21'
    id 'jacoco'
}

repositories {
    maven {
        url "https://repo.specmatic.io/releases"
    }
    maven {
        url "https://packages.confluent.io/maven/"
    }
    mavenCentral()
    mavenLocal()
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "17"
        compilerOptions.languageVersion.set(KotlinVersion.KOTLIN_2_2)
        compilerOptions.apiVersion.set(KotlinVersion.KOTLIN_2_2)
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        csv.required = true
    }
}

def specmaticOpenAPIVersion = project.property("specmaticOpenAPIVersion")
def specmaticKafkaVersion = project.property("specmaticKafkaVersion")

dependencies {
    implementation platform('org.springframework.boot:spring-boot-dependencies:3.5.7')
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.apache.kafka:kafka-clients:4.1.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'

    testApi "io.specmatic.openapi:specmatic-openapi:$specmaticOpenAPIVersion"
    testImplementation("io.specmatic.async:specmatic-async-core-min:$specmaticKafkaVersion")
    testImplementation("io.specmatic.async:specmatic-kafka:$specmaticKafkaVersion") {
        exclude group: 'io.specmatic.async', module: 'specmatic-async-core'
    }

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation('org.assertj:assertj-core:3.27.6', 'org.junit.jupiter:junit-jupiter-api:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.1'
    testImplementation "org.testcontainers:junit-jupiter:1.21.3"
}

test {
    useJUnitPlatform {
        includeEngines("junit-jupiter")
    }
    testLogging.showStandardStreams = true

    afterSuite { desc, result ->
        if (!desc.parent) {
            def resultLine = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            def lineLength = resultLine.length()
            def separator = "-" * lineLength

            println separator
            println resultLine
            println separator
        }
    }

}

group = 'io.specmatic.sample'
version = '1.0-SNAPSHOT'
sourceCompatibility = '17'

bootJar {
    archiveClassifier = "all"
    version = ""
}
