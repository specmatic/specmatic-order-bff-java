import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.3.0'
    id 'org.springframework.boot' version '3.5.9'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.3.0'
    id 'jacoco'
}

repositories {
    mavenLocal()
    maven {
        url "https://repo.specmatic.io/releases"
    }
    maven {
        url "https://packages.confluent.io/maven/"
    }
    mavenCentral()
}

jacocoTestReport {
    dependsOn test
    reports {
        csv.required = true
    }
}

dependencies {
    implementation platform('org.springframework.boot:spring-boot-dependencies:3.5.9')
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.kafka:spring-kafka:3.3.11'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'

    testImplementation("io.specmatic.openapi:specmatic-openapi:$specmaticOpenAPIVersion")
    testImplementation("io.specmatic.async:specmatic-async:$specmaticAsyncVersion")

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation('org.assertj:assertj-core:3.27.6', 'org.junit.jupiter:junit-jupiter-api:5.14.1')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.1'
    testImplementation "org.testcontainers:junit-jupiter:1.21.4"

    // vulnerable deps
    testImplementation("at.yawk.lz4:lz4-java:1.10.2")
    testImplementation("org.bitbucket.b_c:jose4j:0.9.6")
}

test {
    useJUnitPlatform {
        includeEngines("junit-jupiter")
    }
    testLogging.showStandardStreams = true

    afterSuite { desc, result ->
        if (!desc.parent) {
            def resultLine = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            def lineLength = resultLine.length()
            def separator = "-" * lineLength

            println separator
            println resultLine
            println separator
        }
    }

}

group = 'io.specmatic.sample'
version = '1.0-SNAPSHOT'

tasks.register('dockerBuild', Exec) {
    group = 'docker'
    description = 'Assembles the jar, and builds the Docker image'
    workingDir projectDir
    dependsOn('assemble')
    commandLine 'docker', 'build', '-t', 'specmatic/specmatic-order-bff-java:latest', '.'
}

bootJar {
    archiveClassifier = "all"
    version = ""
}

configurations {
    implementation {
        // Exclude vulnerable dependencies brought in by other libraries
        // this is replaced by `at.yawk.lz4:lz4-java`
        exclude group: 'org.lz4', module: 'lz4-java'
    }
}
